class WolfCeilingLight : Actor
{
	mixin SurfaceFinder;
	class<actor> shine;
	property shine:shine;
	int OnFrame;
	int OffFrame;
	property OnFrame:OnFrame;
	property OffFrame:OffFrame;
	int ceilingOffset;
	Default
	{
		//$Category Wolfendoom
		//$title "wolf lamp green"
		WolfCeilingLight.shine "WolfLampLight";
		WolfCeilingLight.OnFrame 0;
		WolfCeilingLight.OffFrame 1;
		+SPAWNCEILING
		+MOVEWITHSECTOR
		+NOGRAVITY
		radius 16;
		height 10;
	}
	States
	{
		Spawn:
			WLMP A -1;
			Stop;
	}
	Override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		
		vector3 C = GetCeilingPos();
		ceilingOffset = abs((self.pos.Z) - C.Z);
		self.SetOrigin(C - (0, 0, ceilingOffset), FALSE);
		let Light = Spawn(self.shine, self.pos);
		if(Light != NULL)
		{
			Light.Master = self;
			self.target = Light;
		}
	}
	Override void Tick()
	{
		super.Tick();
		self.SetOrigin(GetCeilingPos() - (0, 0, ceilingOffset), TRUE);	
		if(CurSector && CurSector.lightlevel < 128)
			self.LightLevel = 160;
		else
			self.LightLevel = 256;

		if(target)
		{
			if(self.LightLevel == 160)
			{
				target.binvisible = true;
				frame = self.OffFrame;
			}
			else
			{
				target.binvisible = false;
				frame = self.OnFrame;
			}
		}
	}
}

class WolfCeilingLight_Red : WolfCeilingLight
{
	Default
	{
		//$Category Wolfendoom
		//$title "wolf lamp red"
		WolfCeilingLight.OnFrame 2;
		WolfCeilingLight.OffFrame 3;
	}
	States
	{
		Spawn:
			WLMP C -1;
			Stop;
	}
}

class WolfFloorLight : Actor
{
	mixin SurfaceFinder;
	class<actor> shine;
	property shine:shine;
	int OnFrame;
	int OffFrame;
	property OnFrame:OnFrame;
	property OffFrame:OffFrame;
	Default
	{
		//$Category Wolfendoom
		//$title "wolf floor lamp green"
		WolfFloorLight.shine "WolfLampCeilingLight";
		WolfFloorLight.OnFrame 0;
		WolfFloorLight.OffFrame 1;
		+SOLID
		radius 16;
		height 42;
	}
	States
	{
		Spawn:
			WFLP A -1;
			Stop;
	}
	Override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		let Light = Spawn(self.shine, self.pos);
		if(Light != NULL)
		{
			Light.Master = self;
			self.target = Light;
		}
	}
	Override void Tick()
	{
		super.Tick();
		if(CurSector && CurSector.lightlevel < 128)
			self.LightLevel = 160;
		else
			self.LightLevel = 256;

		if(self.LightLevel == 160)
		{
			target.binvisible = true;
			frame = self.OffFrame;
		}
		else
		{
			target.binvisible = false;
			frame = self.OnFrame;
		}
	}
}

class WolfFloorLight_Red : WolfFloorLight
{
	Default
	{
		//$Category Wolfendoom
		//$title "wolf floor lamp red"
		WolfFloorLight.OnFrame 2;
		WolfFloorLight.OffFrame 3;
	}
	States
	{
		Spawn:
			WFLP C -1;
			Stop;
	}
}

Class WolfLampLight : Actor
{
	mixin SurfaceFinder;
	Default
	{
		Radius 8;
		Height 0.25;
		+NOBLOCKMAP
		+MOVEWITHSECTOR
		+BRIGHT
		+FLATSPRITE
		+NOINTERACTION
		RenderStyle "ADD";
		Alpha 0.125;
	}
	Override Void PostBeginPlay()
	{
		if(CheckForSky(TRUE))
			self.Destroy();
		self.SetOrigin(GetFloorPos() + (0, 0, self.height), FALSE);
		Actor.PostBeginPlay();
	}

	Override void Tick()
	{
		super.Tick();
		self.SetOrigin(GetFloorPos() + (0, 0, self.height), TRUE);
		if(master && Abs(self.pos.Z-master.pos.Z) > 192)
			self.binvisible = true;
		else if(CurSector.lightlevel < 128)
			self.binvisible = true;
		else
		{
			self.binvisible = false;
			self.Alpha = 0.125;
		}
	}
	States
	{
		Spawn:
			WLMP Z -1;
			Stop;
	}
}

Class WolfLampCeilingLight : WolfLampLight
{
	
	Default
	{
		-MOVEWITHSECTOR
		+NOGRAVITY
		RenderStyle "ADD";
		Alpha 0.125;
	}
	Override Void PostBeginPlay()
	{
		Actor.PostBeginPlay();
		if(CheckForSky())
			self.Destroy();
		self.SetOrigin(GetCeilingPos() - (0, 0, self.height), FALSE);
	}
	Override void Tick()
	{
		actor.Tick();
		self.SetOrigin(GetCeilingPos() - (0, 0, self.height), TRUE);
		if(master && Abs(self.pos.Z-master.pos.Z) > 192)
		{
			self.binvisible = true;
		}
		else if(CurSector.lightlevel < 128)
		{
			self.binvisible = false;
			self.Alpha = 0.125/2;
		}
		else
		{
			self.binvisible = false;
			self.Alpha = 0.125;
		}
	}
}