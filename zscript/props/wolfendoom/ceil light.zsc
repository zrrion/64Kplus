class WolfCeilingLight : Actor
{
	mixin SurfaceFinder;
	class<actor> shine;
	property shine:shine;
	int ceilingOffset;
	Default
	{
		//$Category Wolfendoom
		//$title "wolf lamp green"
		WolfCeilingLight.shine "WolfLampLight";
		-SPAWNCEILING
		+MOVEWITHSECTOR
		+NOGRAVITY
		radius 16;
		height 10;
	}
	States
	{
		Spawn:
			WLMP A -1;
			Stop;
	}
	Override void PostBeginPlay()
	{
		self.SetOrigin(GetCeilingPos(), FALSE);
		Super.PostBeginPlay();
		let Light = Spawn(self.shine, self.pos);
		if(Light != NULL)
		{
			Light.Master = self;
			self.target = Light;
		}
	}
	Override void Tick()
	{
		super.Tick();
		self.SetOrigin(GetCeilingPos(), TRUE);	
		if(S && S.lightlevel < 128)
			self.LightLevel = 160;
		else
			self.LightLevel = 256;
	}
}

class WolfCeilingLight_Red : WolfCeilingLight
{
	Default
	{
		//$Category Wolfendoom
		//$title "wolf lamp red"
	}
	States
	{
		Spawn:
			WLMP B -1;
			Stop;
	}
}

class WolfFloorLight : Actor
{
	mixin SurfaceFinder;
	class<actor> shine;
	property shine:shine;
	Default
	{
		//$Category Wolfendoom
		//$title "wolf floor lamp green"
		WolfFloorLight.shine "WolfLampCeilingLight";
		+SOLID
		radius 16;
		height 42;
	}
	States
	{
		Spawn:
			WFLP A -1;
			Stop;
	}
	Override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		GetFloorPos();
		let Light = Spawn(self.shine, self.pos);
		if(Light != NULL)
		{
			Light.Master = self;
			self.target = Light;
		}
	}
	Override void Tick()
	{
		super.Tick();
		if(S.lightlevel < 128)
			self.LightLevel = 160;
		else
			self.LightLevel = 256;
	}
}

class WolfFloorLight_Red : WolfFloorLight
{
	Default
	{
		//$Category Wolfendoom
		//$title "wolf floor lamp red"
	}
	States
	{
		Spawn:
			WFLP B -1;
			Stop;
	}
}

Class WolfLampLight : Actor
{
	mixin SurfaceFinder;
	Default
	{
		Radius 8;
		Height 0.25;
		+NOBLOCKMAP
		+MOVEWITHSECTOR
		+BRIGHT
		+FLATSPRITE
		+NOINTERACTION
		RenderStyle "ADD";
		Alpha 0.125;
	}
	Override Void PostBeginPlay()
	{
		self.SetOrigin(GetFloorPos(), FALSE);
		Actor.PostBeginPlay();
	}

	Override void Tick()
	{
		super.Tick();
		self.SetOrigin(GetFloorPos(), TRUE);
		if(S.lightlevel < 128 || (master && (self.pos-master.pos).length() > 128))
			self.Alpha = 0.125/2;
		else
			self.Alpha = 0.125;
	}
	States
	{
		Spawn:
			//WLMP B -1;
			WLMP C -1;
			Stop;
	}
}

Class WolfLampCeilingLight : WolfLampLight
{
	
	Default
	{
		-MOVEWITHSECTOR
		+NOGRAVITY
		RenderStyle "ADD";
		Alpha 0.125;
	}
	Override Void PostBeginPlay()
	{
		Actor.PostBeginPlay();
		self.SetOrigin(GetCeilingPos(), FALSE);
	}
	Override void Tick()
	{
		super.Tick();
		self.SetOrigin(GetCeilingPos(), TRUE);
	}
}