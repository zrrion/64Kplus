class DemonDog : actor
{
	
	mixin BerserkDroper;

	Default
	{
		//$Category Monsters
		Health 50;
		PainChance 200;
		Speed 10;
		Radius 20;
		Height 32;
		Mass 75;
		Meleedamage 3;
		Meleerange 40;
		Meleesound "dog/attack";
		Maxtargetrange 192;
		Meleethreshold 96;
		Monster;
		+FLOORCLIP
		+DROPOFF
		SeeSound "dog/sight";
		PainSound "dog/pain";
		DeathSound "dog/death";
		ActiveSound "dog/active";
		AttackSound "dog/attack";
		Obituary "%o was mauled by a Rottweiler.";
	}
	States
	{
	Spawn:
		ROTI A 10 A_Look();
		Loop;
	See:
		ROTD AABBCCDD 2 Fast A_Chase();
		Loop;
	Missile:
		ROTD A 2 A_FaceTarget();
		ROTD A 0 A_StartSound("dog/attack");
		ROTD A 0 ThrustThingZ (0, 24, 0, 0);
		ROTD A 0 ThrustThing (angle*256/360, 12, 0, 0);
	JumpUp:
		ROTD B 8;
	JumpDown:
		ROTD C 1 A_CheckFloor ("Land");
		loop;
	Land:
		ROTD D 2 A_Stop();
		goto See;
	Melee:
		ROTD EF 4 Fast A_FaceTarget();
		ROTD G 4 Fast A_CustomMeleeAttack(3, "dog/attack");
		ROTD FE 4 Fast A_FaceTarget();
		Goto See;
	Pain:
		ROTD A 2;
		ROTD A 2 A_Pain();
		Goto See;
	Death.GloryKill:
		TNT1 A 0 
		{
			for(int i = 0; i < 5; i++)
				{
					A_PowerKillDrops("ArmorBonus",1, Abs(self.painchance-256));
					A_PowerKillDrops("HealthBonus",1, Abs(self.painchance-256));
				}
		}
	Xdeath:
		TNT1 A 0 A_SpawnItemEx("skp_BloodMist", 0, 0, 0, random(-2,2), random(-2,2), 5, 0, SXF_USEBLOODCOLOR|SXF_NOCHECKPOSITION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERALPHA);
	Death:
		ROTD H 4 A_Scream();
		ROTD I 4 A_NoBlocking();
		ROTD J 4;
		ROTD K -1;
		Stop;
	Raise:
		ROTD KJIH 4;
		Goto See;
	}
}