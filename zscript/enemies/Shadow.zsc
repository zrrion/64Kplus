class Shadow : actor
{
	int ChaseCount;
	
	mixin BerserkDroper;
	
	Default
	{
		//$Category Monsters
		Health 70;
		Radius 20;
		Height 56;
		Mass 100;
		RenderStyle "Translucent";
		Alpha 0.7;
		Speed 8;
		PainChance 200;
		Monster;
		+FLOORCLIP
		PainSound "Shadow/pain";
		DeathSound "Shadow/death";
		ActiveSound "Shadow/active";
		Obituary "%o was assassinated by a shadow.";

		Dropitem "$vend_shadow", VEND_DESC;
	}
	States
	{
	Spawn:
		SHDW E 15;
	See:
	See.Chase:
		SHDW AAABBBCCCDDDAAABBBCCCDDD 1 A_Chase();
		Goto See.Chase;
	Missile:
		SHDW EEEEEEEEEEEEEEE 2 A_FaceTarget();
		SHDW F 5 A_FaceTarget();
		SHDW G 5 BRIGHT A_SpawnProjectile("ShadowBall");
		SHDW F 5 A_FaceTarget();
		SHDW EEEEEEEEEE 2 A_FaceTarget();
		SHDW E 1 A_SpidRefire();
		SHDW A 0 A_Jump(64,"See");
		Goto Missile+15;
	Pain:
		SHDW H 4;
		SHDW H 4 A_Pain();
		Goto See;
	Death:
		SHDW I 6 A_Scream();
		SHDW J 5;
		SHDW K 5 A_SpawnItemEx("Shadowtorso", 0, 0, 0, 1, 0, 0, 0, 128);
		SHDW L 5 A_NoBlocking;
		SHDW MNO 6;
		SHDW P -1;
		Stop;
	Death.GloryKill:
		TNT1 A 0 
		{
			for(int i = 0; i < 5; i++)
				{
					A_PowerKillDrops("ArmorBonus",1, Abs(self.painchance-256));
					A_PowerKillDrops("HealthBonus",1, Abs(self.painchance-256));
				}
		}
	Xdeath:
		TNT1 A 0 A_SpawnItemEx("skp_BloodMist", 0, 0, self.height * 0.75, random(-2,2), random(-2,2), 5, 0, SXF_USEBLOODCOLOR|SXF_NOCHECKPOSITION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERALPHA);
		SHDX A 8;
		SHDX B 6 A_XScream();
		SHDX C 6;
		SHDX D 6 A_NoBlocking();
		SHDX EF 6;
		SHDX G -1;
		Stop;
	Raise:
		SHDW MKLJI 8;
		Goto See;
	}
	Override void PostBeginPlay()
	{
		ChaseCount = 0;
		super.PostBeginPlay();
	}
}

extend class Shadow
{
	void A_Chase()
	{
		self.ChaseCount++;
		if(self.ChaseCount >= 3)
		{
			self.ChaseCount = 0;
			A_SpawnItemEx("ShadowGhost", 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERSPRITEFRAME, 128);
		}
		super.A_Chase();
	}
}

class ShadowGhost : actor
{
	Default
	{
		+NOBLOCKMAP
		+NOGRAVITY
		+NOINTERACTION
		RenderStyle "Translucent";
		Alpha 0.3;
	}
	States
	{
	Spawn:
		SHDW "#" 10;
		Stop;
	}
}

class ShadowBall : actor
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 12;
		Damage 4;
		Projectile;
		+RANDOMIZE
		+BRIGHT
		RenderStyle "ADD";
		Alpha 0.75;
		SeeSound "Shadow/attack";
		DeathSound "imp/shotx";
		Decal "DoomImpScorch";
	}
	States
	{
	Spawn: 
		SBAL ABC 4 A_SpawnItemEx("ShadowTrail", 0, 0, 0, 0, 0, 0, 0, 128);
		Loop;
	Death:
		SBAL C 5;
		SBAL DEFGH 4;
		Stop;
	}
}

class Shadowtorso : actor
{
	Default
	{
		Radius 12;
		Height 15;
		Speed 1;
		Mass 5000;
		RenderStyle "Translucent";
		alpha 0.7;
		BounceType "Doom";
	}
	States
	{
	Spawn:
         SHDW QRS 5;
         SHDW T 5 A_NoBlocking();
         SHDW U 5;
         SHDW V -1;
         Stop;
	}
}

class ShadowTrail : ShadowGhost
{
	Default
	{
		RENDERSTYLE "ADD";
		ALPHA 0.6;
	}
	States
	{
	Spawn:
		SHTR ABCDEF 4;
		Stop;
	}
}