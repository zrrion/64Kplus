class skp_Cacodemon : Cacodemon replaces Cacodemon
{
	mixin BerserkDroper;
	
	Default
	{
		//$title "Cacodemon"
		Bloodcolor "Blue";
		Mass 600;
	}
	States
	{
	Death.GloryKill:
		TNT1 A 0 
		{
			for(int i = 0; i < 5; i++)
				{
					A_PowerKillDrops("ArmorBonus",2, Abs(self.painchance-256));
					A_PowerKillDrops("HealthBonus",2, Abs(self.painchance-256));
				}
		}
	Xdeath:
		TNT1 AA 0 A_SpawnItemEx("skp_BloodMist", 0, 0, self.height * 0.75, random(-2,2), random(-2,2), 5, 0, SXF_USEBLOODCOLOR|SXF_NOCHECKPOSITION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERALPHA);
	Death:
		HEAD G 8;
		HEAD H 8 A_Scream;
		HEAD I 8;
		HEAD J 8;
		HEAD K 8 A_NoBlocking;
	Death.Prop:
		HEAD L -1 A_SetFloorClip;
		Stop;
	}
}

class skp_CacodemonBall : CacodemonBall replaces CacodemonBall
{
	Default
	{
		Decal "CacoScorch";
	}
	States
	{
	Spawn:
		BAL2 AABB 2 BRIGHT
		{
			if(CallACS("CVarCheck",2))
				{
					A_SetRoll(Random(0,359));
				}
			A_SpawnItemEx("skp_CacoBallTrail",Random(2,-2),Random(2,-2),Random(2,-2));
			A_SpawnItemEx("ChainsawSmoke",0,0,-4,0,0,fRandom(1,2),0,0,64);
			A_SpawnParticle("000055",SPF_FULLBRIGHT,random(8,16),random(1,3),0,frandom(1,-1)*4,frandom(1,-1)*4,frandom(1,-1),frandom(1,-1),frandom(1,-1),frandom(1,-1));
			A_SpawnParticle("550055",SPF_FULLBRIGHT,random(8,16),random(1,3),0,frandom(1,-1)*4,frandom(1,-1)*4,frandom(1,-1),frandom(1,-1),frandom(1,-1),frandom(1,-1));
			A_SpawnParticle("0000FF",SPF_FULLBRIGHT,random(8,16),random(1,3),0,frandom(1,-1)*4,frandom(1,-1)*4,frandom(1,-1),frandom(1,-1),frandom(1,-1),frandom(1,-1));
			A_SpawnParticle("FF0000",SPF_FULLBRIGHT,random(8,16),random(1,3),0,frandom(1,-1)*4,frandom(1,-1)*4,frandom(1,-1),frandom(1,-1),frandom(1,-1),frandom(1,-1));
		}	
		Loop;
	Death:
		TNT1 A 0 A_SpawnItemEx("ChainsawSmoke",0,0,-4,0,0,fRandom(1,2),0,0);
		BAL2 CDE 6 BRIGHT;
		Stop;
	}
}

class skp_CacoBallTrail : actor
{
	int oldframe;
	Default
	{
		+NOINTERACTION
		+NOGRAVITY
		+NOBLOCKMAP
		+ROLLSPRITE
		+ROLLCENTER
	}
	States
	{
	Spawn:
		TNT1 A 2;
		FLBL IJKLM 2 Bright;
		FLBL M 1 Bright A_Fadeout();
		Wait;
	}
	override Void PostBeginPlay()
	{
		oldframe = frame;
		super.PostBeginPlay();
	}
	override void Tick()
	{
		if(isFrozen())
			return;
		if(oldframe == frame && CallACS("CVarCheck",2))
			A_SetRoll(Random(0,359));
		super.Tick();
	}
}