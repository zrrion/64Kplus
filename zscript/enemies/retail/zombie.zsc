class skp_Zombieman : Zombieman replaces Zombieman
{
	mixin BerserkDroper;
	
	Default
	{
		Dropitem "$vend_zombie", VEND_DESC;
		//$title "Zombie"
	}
	States
	{
	Death:
		POSS H 5;
		POSS I 5 A_Scream;
		POSS J 5 A_NoBlocking;
		POSS K 5;
	Death.Prop:
		POSS L -1;
		Stop;
	Death.GloryKill:
		TNT1 A 0 
		{
			for(int i = 0; i < 5; i++)
				{
					A_PowerKillDrops("ArmorBonus",1, Abs(self.painchance-256));
					A_PowerKillDrops("HealthBonus",1, Abs(self.painchance-256));
				}
		}
	Xdeath:
		TNT1 A 0 A_SpawnItemEx("skp_BloodMist", 0, 0, self.height * 0.75, random(-2,2), random(-2,2), 5, 0, SXF_USEBLOODCOLOR|SXF_NOCHECKPOSITION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERALPHA);
		POSS M 5;
		POSS N 5 A_XScream;
		POSS O 5 A_NoBlocking;
		POSS PQRST 5;
	Xdeath.Prop:
		POSS U -1;
		Stop;
	Raise:
		"----" "#" 0
		{
			if(frame == 20)
				SetStateLabel("Raise.Xdeath");
		}
		POSS L 5;
		POSS KJIH 5;
		Goto See;
	Raise.Xdeath:
		POSS UT 6;
		POSS SRQPONM 4;
		Goto See;
	}
}