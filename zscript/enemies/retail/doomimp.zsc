class skp_DoomImp : DoomImp  replaces DoomImp 
{	
	mixin BerserkDroper;
	
	Default
	{

	}
	States
	{
	Death:
		TROO I 8;
		TROO J 8 A_Scream;
		TROO K 6;
		TROO L 6 A_NoBlocking;
	Death.Prop:
		TROO M -1;
		Stop;
	Death.GloryKill:
		TNT1 A 0 
		{
			for(int i = 0; i < 5; i++)
				{
					A_PowerKillDrops("ArmorBonus",1, Abs(self.painchance-256));
					A_PowerKillDrops("HealthBonus",1, Abs(self.painchance-256));
				}
		}
	Xdeath:
		TNT1 A 0 A_SpawnItemEx("skp_BloodMist", 0, 0, 0, random(-2,2), random(-2,2), 5, 0, SXF_USEBLOODCOLOR|SXF_NOCHECKPOSITION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERALPHA);
		TROO N 5;
		TROO O 5 A_XScream;
		TROO P 5;
		TROO Q 5 A_NoBlocking;
		TROO RST 5;
		TROO U -1;
		Stop;
	Raise:
		"----" A 0
		{
			if(frame == 20)
				SetStateLabel("Raise.Xdeath");
		}
		TROO ML 8;
		TROO KJI 6;
		Goto See;
	Raise.Xdeath:
		TROO UT 6;
		TROO SRQPONM 4;
		Goto See;
	}
}

class skp_DoomImpBall : DoomImpBall replaces DoomImpBall
{
	Default
	{
		Decal "DoomImpScorch";
		+ROLLSPRITE
		+ROLLCENTER
	}
	States
	{
	Spawn:
		BAL1 AB 4 BRIGHT;
		Loop;
	Death:
		BAL1 A 0
		{
			A_SpawnItemEx("ChainsawSmoke",0,0,2*(height/3),0,0,fRandom(1,2));
		}
		BAL1 CDE 6 BRIGHT;
		Stop;
	}
	Override void Tick()
	{
		if(isFrozen())
			return;
		if(frame < 2)
		{
			if(level.totaltime % 4 == 0)
				A_SetRoll(Random(0,359));

			if(level.totaltime % 8 == 0)
				A_SpawnItemEx("ChainsawSmoke",0,0,frandom(-8,-6),0,0,fRandom(1,2),0,0,192);
				
			if(level.totaltime % 2 == 0)
			{
				A_SpawnItemEX("skp_ImpBallTrail",frandom(-4,4),frandom(-4,4),frandom(-4,4));
				A_SpawnParticle("FFFF55",SPF_FULLBRIGHT,random(8,16),random(1,2),0,frandom(1,-1)*4,frandom(1,-1)*4,frandom(1,-1),frandom(1,-1),frandom(1,-1),frandom(1,-1));
				A_SpawnParticle("FF0000",SPF_FULLBRIGHT,random(8,16),random(1,2),0,frandom(1,-1)*4,frandom(1,-1)*4,frandom(1,-1),frandom(1,-1),frandom(1,-1),frandom(1,-1));
				A_SpawnParticle("FF0000",SPF_FULLBRIGHT,random(8,16),random(1,2),0,frandom(1,-1)*4,frandom(1,-1)*4,frandom(1,-1),frandom(1,-1),frandom(1,-1),frandom(1,-1));
			}
		}
		super.Tick();
	}
}

class skp_ImpBallTrail : actor
{
	Default
	{
		+NOINTERACTION
		+NOGRAVITY
		+NOBLOCKMAP
		+ROLLSPRITE
		+ROLLCENTER
		+ZDOOMTRANS
		Renderstyle "ADD";
	}
	States
	{
	Spawn:
		TNT1 A 2;
		IBAL ABCD 1 Bright
		{
			if(CallACS("CVarCheck",2))
			{
				A_SetRoll(Random(0,359));
			}
		}
		IBAL D 1 Bright A_Fadeout(0.2);
		Wait;
	}
}