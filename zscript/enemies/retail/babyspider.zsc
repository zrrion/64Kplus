class skp_Arachnotron : Arachnotron replaces Arachnotron
{
	mixin BerserkDroper;
	
	Default
	{
		//$title "Arachnotron"
		Dropitem "$vend_arachnotron", VEND_DESC;
	}
	States
	{
	Death.GloryKill:
		TNT1 A 0 
		{
			for(int i = 0; i < 5; i++)
				{
					A_PowerKillDrops("ArmorBonus",2, Abs(self.painchance-256));
					A_PowerKillDrops("HealthBonus",2, Abs(self.painchance-256));
				}
		}
	Xdeath:
		TNT1 AA 0 A_SpawnItemEx("skp_BloodMist", 0, 0, self.height * 0.75, random(-2,2), random(-2,2), 5, 0, SXF_USEBLOODCOLOR|SXF_NOCHECKPOSITION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERALPHA);
	Death:
		BSPI J 20 A_Scream;
		BSPI K 7 A_NoBlocking;
		BSPI LMNO 7;
		BSPI P -1 A_BossDeath;
		Stop;
	}
}

class skp_ArachnotronPlasma : ArachnotronPlasma replaces ArachnotronPlasma
{
	bool DoPart;
	Default
	{
		DamageType "Plasma";
		Decal "ArachnotronScorch";
		+ROLLSPRITE
		+ROLLCENTER
	}
	States
	{
	Spawn:
		APLS AB 6 Bright;
		Loop;
	Death:
		TNT1 A 0
		{
			A_SpawnItemEx("ChainsawSmoke",0,0,-4,0,0,fRandom(1,2),0,0);
			bXFLIP = Random(true, false);
			bYFLIP = Random(true, false);
			DoPart = FALSE;
		}
		APBX ABCDE 5 BRIGHT
			{
				if(Cvar.FindCVar("arg_GoreRotation").GetBool())
				{
					A_SetRoll(Random(0,359));
				}
			}
		Stop;
	}
	Override void PostBeginPlay()
	{
		DoPart = TRUE;
		Super.PostBeginPlay();
	}
	Override void Tick()
	{
		if(isFrozen())
			return;
		if(level.totaltime % 2 == 0 && DoPart)
			for(int i = 0; i < 4; i++)
			{
				A_SpawnParticle("FFFF55",SPF_FULLBRIGHT,random(8,16),random(1,2),0,frandom(1,-1)*4,frandom(1,-1)*4,frandom(1,-1),frandom(1,-1),frandom(1,-1),frandom(1,-1));
			}
		if(DoPart)
			A_SpawnItemEx("skp_ArachnotronPlasmaTrail");
		super.Tick();
	}
}

class skp_ArachnotronPlasmaTrail : actor
{
	Default
	{
		+NOINTERACTION
		+NOGRAVITY
		+NOBLOCKMAP
		renderstyle "add";
		alpha 0.4;
	}
states
	{
	Spawn:
		STRA AB 3 Bright;
		STRA C 2 Bright;
		STRA C 1 Bright A_Fadeout;
		Wait;
	}
}