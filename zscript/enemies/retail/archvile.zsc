class skp_Archvile : Archvile replaces Archvile
{
	mixin BerserkDroper;
	
	Default
	{
		//$title "Archvile"
		Dropitem "$vend_vile", VEND_DESC;
	}
	States
	{
	Death.GloryKill:
		TNT1 A 0 
		{
			for(int i = 0; i < 5; i++)
				{
					A_PowerKillDrops("ArmorBonus",3, Abs(self.painchance-256));
					A_PowerKillDrops("HealthBonus",3, Abs(self.painchance-256));
				}
		}
	Xdeath:
		TNT1 AA 0 A_SpawnItemEx("skp_BloodMist", 0, 0, self.height * 0.75, random(-2,2), random(-2,2), 5, 0, SXF_USEBLOODCOLOR|SXF_NOCHECKPOSITION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERALPHA);
	Death:
		VILE Q 7;
		VILE R 7 A_Scream;
		VILE S 7 A_NoBlocking;
		VILE TUVWXY 7;
		VILE Z -1;
		Stop;
	}
}

class skp_ArchvileFire : ArchvileFire replaces ArchvileFire
{
	int HeightOffset;
	Default
	{
	}
	override void Tick()
	{
		if(isFrozen())
			return;
		//the difference in tics from when this spawns to when this stops being a thing just happens to be about the same height as the fire sprite moves upward, so we can move where we are spawning the fire particles based on that
		//we still don't want to add to it unless time is moving
		HeightOffset += 1;
		if(level.totaltime % 2 == 0)
		{
			let RadiusHaver = tracer;
			if(RadiusHaver == null)
				RadiusHaver = self;
			for(int i = 0; i < 4; i++)
			{
				A_SpawnItemEx("AH_Flame",frandom(0,RadiusHaver.radius),0,frandom(0,RadiusHaver.height) + HeightOffset,0,0,frandom(1,1.5),frandom(0,360),0,192);
			}
			A_SpawnItemEx("RocketSmokeTrail",random(0,self.radius),random(-3,-3),random(1,3) + HeightOffset,0,0,frandom(2,2.5),frandom(0,360),0,250);
		}
		super.Tick();
	}
	Override Void PostBeginPlay()
	{
		HeightOffset = 0;
		bXFLIP = random(TRUE, FALSE);
		super.PostBeginPlay();
	}
}