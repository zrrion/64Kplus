class skp_LostSoul : LostSoul replaces LostSoul
{
	mixin BerserkDroper;
	
	int oldframe;
	Default
	{
		//$title "Lost Soul"
	}
	States
	{
	See:
		SKUL AB 6 BRIGHT soul_Chase;
		Loop;
	Death.GloryKill:
		TNT1 A 0 
		{
			for(int i = 0; i < 5; i++)
				{
					A_PowerKillDrops("ArmorBonus",1, Abs(self.painchance-256));
					A_PowerKillDrops("HealthBonus",1, Abs(self.painchance-256));
				}
		}
	Xdeath:
	Death:
		SKUL F 6 BRIGHT A_SpawnItemEx("RocketSmokeTrail",0,0,2*(height/3),0,0,fRandom(1,2),0,0,192);
		SKUL G 6 BRIGHT
		{
			A_Scream();
			A_SpawnItemEx("RocketSmokeTrail",Random(-2,2),Random(-2,2),2*(height/3),0,0,fRandom(1,2));
			A_SpawnItemEx("RocketSmokeTrail",Random(-2,2),Random(-2,2),2*(height/3),0,0,fRandom(1,2));
		}
		SKUL H 6 BRIGHT;
		SKUL I 6 BRIGHT A_NoBlocking;
		SKUL J 6;
		SKUL K 6;
		Stop;
	}
	void soul_Chase()
	{
		if(target != NULL && !target.bISMONSTER && (target.bSHADOW || target.bINVISIBLE))//if they have a target that isn't a monster and the target is invisible then don't chase them. lost souls aren't smart
			A_Wander();
		else
			A_Chase();
	}
	Override void PostBeginPlay()
	{
		oldframe = frame;
		super.PostBeginPlay();
	}
	Override void Tick()
	{
		if(isFrozen())
		return;
		if(frame != oldframe)
			A_SpawnItemEx("ChainsawSmoke",0,0,2*(height/3),0,0,fRandom(1,2),0,0,192);
		oldframe = frame;
		super.Tick();
	}
}