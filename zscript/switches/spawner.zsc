class skp_EnemySpawner : actor
{
	bool user_RadRespawn;
	static const string MOBJ_TYPES_RETAIL[] = 
	{
		"NULL",
		"ZombieMan",
		"ShotgunGuy",
		"DoomImp",
		"Demon",
		"Spectre",
		"LostSoul",
		"ChaingunGuy",
		"Cacodemon",
		"PainElemental",
		"Revenant",
		"Fatso",
		"Arachnotron",
		"Archvile",
		"HellKnight",
		"BaronOfHell",
		"Cyberdemon",
		"SpiderMastermind",
		"WolfensteinSS",
		"ExplosiveBarrel",
		"skp_Ghoul"
	};

	static const string MOBJ_TYPES_SKP[] = 
	{
		"NULL",
		"DemonDog",
		"Shadow",
		"ZombieSS",
		"Cacomera",
		"GoreNest"
	};
	string user_SpawnType;
	actor YerDude;
	default
	{
		//$Category Actions
		//$title "Enemy Spawner"
        //$Arg0 "Enemy Spawn Type (retail)"
        //$Arg0Type 11
        //$Arg0Enum { 0 = "None"; 1 = "Zombie Man"; 2 = "Shotgun Guy"; 3 = "Imp"; 4 = "Demon"; 5 = "Spectre"; 6 = "Lost Soul"; 7 = "Chaingun Guy"; 8 = "Cacodemon"; 9 = "Pain Elemental"; 10 = "Revenant"; 11 = "Mancubus"; 12 = "Arachnotron"; 13 = "Archvile"; 14 = "Hell Knight"; 15 = "Baron of Hell"; 16 = "Cyberdemon"; 17 = "Spider Mastermind"; 18 = "Wolfenstein SS"; 19 = "Explosive Barrel"; 20 = "Goul";}
        //$Arg0Default 0

		//$Arg1 "Enemy Spawn Type (64K+)"
        //$Arg1Type 11
        //$Arg1Enum { 0 = "None"; 1 = "Demon Dog"; 2 = "Shadow"; 3 = "Retro Zombie"; 4 = "Cacomera"; 5 = "Gore Nest";}
        //$Arg1Default 0

		//$Arg2 "Spawn Style"
        //$Arg2Type 11
        //$Arg2Enum { 0 = "Teleport"; 1 = "Fake Instant floor"; 2 = "silent";}
        //$Arg2Default 0

		//$Arg3 "Special Activation"
        //$Arg3Type 11
        //$Arg3Enum { 0 = "once on thing spawn"; 1 = "once on thing death"; 2 = "every thing spawn"; 3 = "every thing death";}
        //$Arg3Default 1
		
		//$Arg4 "Action tid"
        //$Arg4Type 14
        //$Arg4Default 0
		+INVISIBLE;
	}
	Virtual string SpawnWhat()
	{
		args[0] %= 21;
		args[1] %= 6;
		if(user_SpawnType.CodePointCount())
			return user_SpawnType;
		else if(args[1])
			return MOBJ_TYPES_SKP[args[1]];
		else
			return MOBJ_TYPES_RETAIL[args[0]];
		return "";
	}
	Override void Activate(Actor activator)
	{
		string MobjToSpawn = SpawnWhat();
		if(LorWeapons && MobjToSpawn == "skp_PlasmaOffer")
			MobjToSpawn = "skp_IncOffer";
		if(MobjToSpawn == "NULL" || MobjToSpawn == "")
			return;
		let YD = spawn(MobjToSpawn, self.pos, ALLOW_REPLACE);
		if(YD)
		{
			YerDude = YD;
			if(RadSuit(YerDude) && !user_RadRespawn)
				RadSuit(YerDude).bALWAYSRESPAWN = false;
			YerDude.Angle = self.angle;
			if(args[2] == 0)
				spawn("TeleportFog", self.pos, ALLOW_REPLACE);
			if(args[2] == 1)
			{
				if(GetAge() % 3 == 0)
					YerDude.SpriteOffset.Y += YerDude.height * 0.25;
				else if(GetAge() % 2 == 0)
					YerDude.SpriteOffset.Y += YerDude.height * 0.5;
				else
					YerDude.SpriteOffset.Y += YerDude.height * 0.75;
				self.bSUMMONEDMONSTER = true;
				A_StartSound("plats/pt1_stop");
			}

			if(args[4])
			{
				if(args[3] == 0)
				{
					A_CallSpecial(130, self.args[4], 0, 0, 0, 0);
					self.args[4] = 0;
				}
				if(args[3] == 1)
				{
					YerDude.special = 130;
					YerDude.args[0] = self.args[4];
					self.args[4] = 0;
				}
				if(args[3] == 2)
				{
					A_CallSpecial(130, self.args[4], 0, 0, 0, 0);
				}
				if(args[3] == 3)
				{
					YerDude.special = 130;
					YerDude.args[0] = self.args[4];
				}
			}
			YerDude.bDROPPED = false;
			Yerdude.bAMBUSH = self.bAMBUSH;
		}
	}
	Override void Deactivate(Actor activator)
	{
		if(!activator)
			return;
		super.Activate(activator);
	}
	Override Void Tick()
	{
		if(bSUMMONEDMONSTER && !IsFrozen() && YerDude && YerDude.GetAge() > 0)
		{
			YerDude.SpriteOffset.Y = 0;
			self.bSUMMONEDMONSTER = false;
			self.YerDude = NULL;
		}
		super.Tick();
	}
	states
	{
		spawn:
		activate:
		deactivate:
			ACTN H 1;
			wait;
	}
}

class skp_ItemSpawner : skp_EnemySpawner
{
	static const string ITEM_TYPES_RETAIL[] = 
	{
		"NULL",
		"HealthBonus",//health
		"Stimpack",
		"Medikit",
		"ArmorBonus",//armor
		"GreenArmor",
		"BlueArmor",
		"Clip",//ammo
		"ClipBox",
		"Shell",
		"ShellBox",
		"RocketAmmo",
		"RocketBox",
		"Cell",
		"CellPack",
		"Backpack",
		"Chainsaw",//weapons
		"Pistol",
		"Shotgun",
		"SuperShotgun",
		"Chaingun",
		"RocketLauncher",
		"PlasmaRifle",
		"BFG9000",
		"BlueCard",//keys
		"RedCard",
		"YellowCard",
		"BlueSkull",
		"RedSkull",
		"YellowSkull",
		"Allmap",//powerups
		"Berserk",
		"BlurSphere",
		"Infrared",
		"InvulnerabilitySphere",
		"Megasphere",
		"SoulSphere",
		"RadSuit"//37
	};

	static const string ITEM_TYPES_SKP[] = 
	{
		"NULL",
		"MiniStimpack",//health
		"SKP_HealthMegaBonus",
		"SKP_GreenBoots",//armor
		"SKP_RedArmor",
		"skp_SawFuel",//ammo
		"skp_unmaker",//weapons
		"skp_GreenSkull",//keys
		"PurpleCard",
		"SKP_JumpBoots",//powerups
		"SKP_JumpBoots_Perm",//10
		"skp_PlasmaOffer"
	};
	default
	{
		//$Category Actions
		//$title "Item Spawner"
        //$Arg0 "Item Spawn Type (retail)"
        //$Arg0Type 11
        //$Arg0Enum { 0 = "None"; 1 = "Health bonus"; 2 = "Stimpack"; 3 = "Medikit"; 4 = "Armor Bonus"; 5 = "Green Armor"; 6 = "Blue Armor"; 7 = "Clip"; 8 = "Clip Box"; 9 = "Shells"; 10 = "Shell Box"; 11 = "Rocket"; 12 = "Rocket Box"; 13 = "Cells"; 14 = "CellPack"; 15 = "Backpack"; 16 = "Chainsaw"; 17 = "Pistol"; 18 = "Shotgun"; 19 = "Super Shotgun"; 20 = "Chaingun"; 21 = "Rocket Launcher"; 22 = "Plasma Rifle" ; 23 = "BFG 9000"; 24 = "Blue Keycard"; 25  = "Red Keycard"; 26 = "Yellow Keycard"; 27 = "Blue Skull Key"; 28 = "Red Skull Key"; 29 = "Yellow Skull Key"; 30 = "Area Map"; 31 = "Berserk Pack"; 32 = "Blur Sphere"; 33 = "Light Amp. Goggles"; 34 = "Invulnerability Sphere"; 35 = "Megasphere"; 36 = "Soul Sphere"; 37 = "Radiation Suit";}
        //$Arg0Default 0

		//$Arg1 "Item Spawn Type (64K+)"
        //$Arg1Type 11
        //$Arg1Enum { 0 = "None"; 1 = "Mini Stimpack"; 2 = "Large Health Bonus"; 3 = "Green Boots"; 4 = "Red Armor"; 5 = "Chainsaw Fuel"; 6 = "Unmaker"; 7 = "Green Skull Key"; 8 = "Purple Keycard"; 9 = "Double-Jump Boots (end of map/hub)"; 10 = "Double-Jump Boots (permanent)"; 11 = "Plasma Offer"; 12 = "Incinerator Offer";}
        //$Arg1Default 0

		//$Arg2 "Spawn Style"
        //$Arg2Type 11
        //$Arg2Enum { 0 = "Teleport"; 1 = "Fake Instant floor"; 2 = "silent";}
        //$Arg2Default 0

		//$Arg3 "Special Activation"
        //$Arg3Type 11
        //$Arg3Enum { 0 = "once on item spawn"; 1 = "once on item pickup"; 2 = "every item spawn"; 3 = "every item death";}
        //$Arg3Default 1
		
		//$Arg4 "Action tid"
        //$Arg4Type 14
        //$Arg4Default 0
	}
	Override string SpawnWhat()
	{
		args[0] %= 38;
		args[1] %= 13;
		if(user_SpawnType.CodePointCount())
			return user_SpawnType;
		else if(args[1])
			return ITEM_TYPES_SKP[args[1]];
		else
			return ITEM_TYPES_RETAIL[args[0]];
		return "";
	}
	states
	{
		spawn:
		activate:
		deactivate:
			ACTN I 1;
			wait;
	}
}