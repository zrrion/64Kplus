class CacoSwitch : SwitchBase
{
	//$Category Switches
	actor NoShootMe;
	Default
	{
		//$Category Switches
		height 50;
		radius 32;
		-FIXMAPTHINGPOS;
		+SHOOTABLE;
		+WALLSPRITE;
		+NOBLOODDECALS;
		+NOICEDEATH;
		+DONTGIB;
		+DONTTHRUST;
		+SOLID;
		Bloodcolor "Blue";
		PainSound "caco/pain";
	}
	Override int DamageMobj(Actor inflictor, Actor source, int damage, Name mod, int flags, double angle)
	{
		if(!bNOPAIN && random(0,1))
			SetStateLabel("Pain");
		return 0;
	}
	Override bool CanCollideWith(Actor other, bool passive)
	{
		if(playerpawn(other))
			NoShootMe = other;
		if(self.bSHOOTABLE && other.bMISSILE && !other.bRIPPER)
		{
			self.DamageMobj(other, other, 666, "none", 0, 0);
			return false;
		}
		return false;
	}
	Override void Tick()
	{
		super.Tick();
		if(NoShootMe)
		{
			if(Distance2D(NoShootMe) > 256)
				NoShootMe = NULL;
			bSHOOTABLE = (NoShootMe && Distance2D(NoShootMe) > 72);
		}
		else
		{
			bSHOOTABLE = true;
		}
	}
	States
	{
	InactiveB:
		//TNT1 A 0 {	bNOPAIN = true;	}
		SWCD B 70;
		TNT1 A 0
		{
			Flip = Flip ? FALSE : TRUE;
		}
	InactiveA:
	Spawn:
		TNT1 A 0 {	bNOPAIN = false;	}
		SWCD A 1;
		wait;
	NoFlip:
		//TNT1 A 0	{	bNOPAIN = true;	}
		SWCD B 70;
		//SWCD B 0 {	bSHOOTABLE = false;	}
		SWCD B -1;
		Stop;
	Pain:
		TNT1 A 0	{	bNOPAIN = true;	}
		SWCD C 8 A_Pain();
		TNT1 A 0
		{
				bNOPAIN = false;
				if(!Flip && bUSESPECIAL)
					SetStateLabel("spawn");
				else
					SetStateLabel("NoFlip");
		}
		Goto Spawn;
	}
}