class SKP_TeleportFog : TeleportFog replaces TeleportFog
{
	int HeightOffset;
	bool DoFire;
	default
	{
		+BRIGHT
		+ZDOOMTRANS
	}
	override void PostBeginPlay ()
	{
		HeightOffset = 0;
		switch (CallACS("CVarCheck",5))
		{
			case 0:
				A_StartSound("misc/teleportREAL",CHAN_BODY);
				Break;
			case 1:
				SetStateLabel("Spawn.HellFire");
				Break;
			case 2:
				A_StartSound("weapons/plasmax",CHAN_BODY);
				SetStateLabel("Spawn.Smoke");
				Break;
			case 3:
				A_StartSound("misc/teleport/raven",CHAN_BODY);
				SetStateLabel("Spawn.Raven");
				Break;
			case 4:
			default:
				A_StartSound("misc/teleport/strife",CHAN_BODY);
				SetStateLabel("Spawn.Strife");
				Break;
		}
	}
	override void Tick()
	{
		if(isFrozen())
			return;
		HeightOffset += 2;
		if(level.totaltime % 2 == 0 && DoFire)
		{
			for(int i = 0; i < 4; i++)
			{
				A_SpawnItemEx("AH_Flame",frandom(0,self.radius),0,frandom(0,self.height) + HeightOffset,0,0,frandom(1,1.5),frandom(0,360),0,192);
			}
			A_SpawnItemEx("RocketSmokeTrail",random(0,self.radius),random(-3,-3),random(1,3) + HeightOffset,0,0,frandom(2,2.5),frandom(0,360),0,250);
		}
		super.Tick();
	}
	States
		{
			Spawn.HellFire:
				Spawn.HellFire:
				FIRE A 2
				{
					A_StartSound("vile/firestrt",CHAN_BODY);
					DoFire = TRUE;
				}
				FIRE BBBBCC 1;
				FIRE C 2 A_StartSound("vile/firecrkl",CHAN_BODY);
				FIRE CCCDDDDDEEEEE 1;
				FIRE F 2 A_StartSound("vile/firecrkl",CHAN_BODY);
				FIRE FGGGHHH 1;
				Stop;
			Spawn.Smoke:
				TNT1 AAAAAAAA 0 A_SpawnItemEx("ChainsawSmoke",fRandom(8,-8),fRandom(8,-8),fRandom(0,1.5),0,0,fRandom(1,2));
				TNT1 AAAA 0 A_SpawnItemEx("RocketSmokeTrail",fRandom(8,-8),fRandom(8,-8),fRandom(0,1.5),0,0,fRandom(1,2));
				Stop;
			Spawn.Raven:
				RTFG ABCDEFGHGFEDC 6;
				Stop;
			Spawn.Strife:
				STFG ABCDEFEDCB 3;
				Stop;
		}
}
	
//====
class SKP_ItemFog : ItemFog replaces ItemFog
{
	default
	{
		+BRIGHT
		+ZDOOMTRANS
		Renderstyle "Add";
	}
	override void PostBeginPlay ()
	{
		if(CallACS("CVarCheck",2))
				{
					bROLLSPRITE = TRUE;
					bROLLCENTER = TRUE;
					bXFLIP = random(FALSE,TRUE);
					bYFLIP = random(FALSE,TRUE);
					A_SetRoll(Random(0,359));
				}
		Super.PostBeginPlay();
	}
}