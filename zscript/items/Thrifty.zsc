extend class skp_DoomPlayer
{
	override bool CanReceive(Inventory item)
	{
		bool SuperRet = super.CanReceive(item);
		if(item.GetAge() <= 1)
		{
			return SuperRet;
		}

		//ordered like this based on what I expect will get picked up most often
		bool TRA = TryReceiveAmmo(item);
		bool TRH = TryReceiveHealth(item);
		bool TRAr = TryReceiveArmor(item);
		bool TRW = TryReceiveWeapon(item);
		if(TRA || TRH || TRAr || TRW || item.bALWAYSPICKUP)
		{
			if(item.bNOSKIN)
			{
				item.bNOSKIN = false;// it can have its skin back as a treat
				return false;
			}
			if(SuperRet)
			{
				string SayTheLine = StringTable.Localize(item.PickupMessage());
				string Ammo1;
				string Ammo2;
				if(Weapon(item))
				{
					let YerGun = Weapon(item);
					SayTheLine = StringTable.Localize(YerGun.PickupMessage());
					if(item.bSUMMONEDMONSTER)
					{
						DropItem di = item.GetDropItems();
						actor OpenedItem;
						if(di)
							OpenedItem = A_DropItem(di.Name);
						if(OpenedItem && OpenedItem.Obituary.CodePointCount())
							SayTheLine = OpenedItem.Obituary;
						else
							SayTheLine = "You unloaded a weapon.";;
					}
					if(YerGun.AmmoGive1)
						Ammo1 = string.Format(" (+%i)", YerGun.AmmoGive1);
					if(YerGun.AmmoGive2)
						Ammo2 = string.Format(" (+%i)", YerGun.AmmoGive2);
				}
				else if(Ammo(item))//the flags do whatever I tell them to do okay
				{
					if(item.bSUMMONEDMONSTER)
					{
						DropItem di = item.GetDropItems();
						actor OpenedItem;
						if(di && di.Probability == -1)
							OpenedItem = spawn(di.name, item.pos);
						else if(di)
							OpenedItem = A_DropItem(di.Name);
						if(OpenedItem && OpenedItem.Obituary.CodePointCount())
						{
							OpenedItem.target = self;
							SayTheLine = OpenedItem.Obituary;
						}
						else
							SayTheLine = "You opened a box of ammo.";
					}
					Ammo1 = string.Format(" (+%i)", item.Amount);
				}
				else if(Health(item))
				{
					if(item.bSUMMONEDMONSTER)
					{
						DropItem di = item.GetDropItems();
						actor OpenedItem;
						if(di)
							OpenedItem = A_DropItem(di.Name);
						if(OpenedItem && OpenedItem.Obituary.CodePointCount())
						{
							OpenedItem.target = self;
							SayTheLine = OpenedItem.Obituary;
						}
						else
							SayTheLine = "You opened an aid kit.";
					}
					Ammo1 = string.Format(" (+%i)", item.Amount);
				}
				else if(BasicArmorBonus(item))
					Ammo1 = string.Format(" (+%i)", BasicArmorBonus(item).SaveAmount);
				else if(BasicArmorPickup(item))
					Ammo1 = string.Format(" (+%i)", BasicArmorPickup(item).SaveAmount);
				SayTheLine = string.Format("%s\cr%s%s\cl", SayTheLine, Ammo1, Ammo2);

				item.PrintPickupMessage(true, SayTheLine);
				item.PlayPickupSound(self);
				self.player.bonuscount = 6;
				item.bQUIET = true;
				return true;
			}
		}
		if(item is "ammo" || item is "Health" || item is "BasicArmorBonus" || item is "BasicArmorPickup" || item is "Weapon")
			return false;
		return SuperRet;
	}

	/*override void HasReceived(Inventory item)
	{
		if(!item)
			return;
		
		super.HasReceived(item);
	}*/
	bool TryReceiveAmmo(Inventory item)
	{
		let Pickup = Ammo(item);
		if(!Pickup)
			return false;
		
		let SmallPickup = Pickup.GetParentAmmo();
		let Holding = FindInventory(SmallPickup);
		if(!Holding)
			return true;
		int HoldingAmount = Holding.Amount;
		int HoldingMax = Holding ? Holding.MaxAmount : Holding.MaxAmount;
		int HoldingNeed = HoldingAmount >= HoldingMax ? 0 : HoldingMax - HoldingAmount ;
		int PickupAmount = Pickup.bIgnoreSkill || Pickup.bDROPPED ? item.Amount : GetDefaultByType(item.GetClass()).Amount*G_SkillPropertyFloat(SKILLP_AmmoFactor);
		int PickupSmallAmount = GetDefaultByType(SmallPickup).Amount;
		int PickupMaxAmount = Holding.MaxAmount;
		
		if(item.bALWAYSPICKUP || DoNeedFullPickup(HoldingAmount, HoldingMax, PickupAmount, PickupMaxAmount))//we can pick up the entire thing
		{
			item.bNOSKIN = false;
			return true;
		}
		if(DoNeedPickup(HoldingAmount, HoldingMax, PickupAmount, PickupMaxAmount) && GetDefaultByType(item.GetClass()).Amount != PickupSmallAmount)//this is a big pickup and we can't pick the entire thing up but we still need ammo
		{
			item.amount -= HoldingNeed;//remove what we need from the pool
			item.bSUMMONEDMONSTER = true;
			while(item.amount > PickupSmallAmount)
			{
				A_DropItem(SmallPickup, PickupSmallAmount);//drop a small pickup
				item.amount -= PickupSmallAmount;//remove the small pickup amount from the pool
			}
			if(item.amount)//if there is less than a small pickup left then we drop a small pickup with what's left
				A_DropItem(SmallPickup, item.amount);
			item.amount = HoldingNeed;//we set the amount back to what we need
			item.bNOSKIN = false;
			return true;
		}
		item.bNOSKIN = true;
		return false;
	}
	bool TryReceiveHealth(Inventory item)
	{
		let Pickup = Health(item);
		if(!Pickup)
			return false;
		Class<Actor> SmallPickup = 'MiniStimpack';
		int HoldingAmount = self.Health;
		int HoldingMax = self.GetMaxHealth(true);
		int HoldingNeed = HoldingAmount >= HoldingMax ? 0 : HoldingMax - HoldingAmount ;
		int SmallPickupAmount = inventory(GetDefaultByType(SmallPickup)).Amount;
		int PickupMaxAmount = GetDefaultByType(item.GetClass()).MaxAmount;
		PickupMaxAmount += !PickupMaxAmount ? HoldingMax : 0;
		//with the setup done lets get to the real stuff
		if(item.bALWAYSPICKUP || DoNeedFullPickup(HoldingAmount, HoldingMax, Pickup.Amount, PickupMaxAmount))//we can pick up the entire thing
		{
			item.bNOSKIN = false;
			return true;
		}
		if(DoNeedPickup(HoldingAmount, HoldingMax, SmallPickupAmount, PickupMaxAmount) && !(item.GetClass() is 'MiniStimpack'))
		{
			//the default amount the small pickup gets you
			item.amount -= HoldingNeed;//remove what we need from the pool
			item.bSUMMONEDMONSTER = true;
			while(item.amount > SmallPickupAmount)
			{
				A_DropItem(SmallPickup, SmallPickupAmount);//drop a small pickup
				item.amount -= SmallPickupAmount;//remove the small pickup amount from the pool
			}
			if(item.amount)//if there is less than a small pickup left then we drop a small pickup with what's left
				A_DropItem(SmallPickup, item.amount);
			item.amount = HoldingNeed;//we set the amount back to what we need
			item.bNOSKIN = false;
			return true;
		}
		item.bNOSKIN = true;
		return false;
	}
	bool TryReceiveArmor(Inventory item)
	{
		if(item is "BasicArmorBonus" || item is "BasicArmorPickup")
		{
			let armor = BasicArmor(FindInventory("BasicArmor"));
			if(!CountInv("BasicArmor"))
				return true;
			if(BasicArmorPickup(item))
			{
				if (armor && armor.Amount >= BasicArmorPickup(item).SaveAmount + armor.BonusCount)
				{
					return false;
				}
			}
			if(BasicArmorBonus(item))
			{
				if (armor && armor.Amount + CountInv("BasicArmor") > BasicArmorBonus(item).MaxSaveAmount)
				{
					return false;
				}
			}
			return true;
		}
		return false;
	}
	bool TryReceiveWeapon(Inventory item)
	{
		let Pickup = Weapon(item);
		bool NeedGun;
		bool Need1;
		bool Need2;
		if(!Pickup)
			return false;
		if(Pickup.AmmoGive1 && Pickup.AmmoType1)
		{
			let Holding = FindInventory(Pickup.AmmoType1);
			if(Holding)
			{
				int HoldingAmount = Holding ? Holding.Amount : 0;
				int HoldingMax = Holding ? Holding.MaxAmount : GetDefaultByType(Holding.GetClass()).MaxAmount;
				int HoldingNeed = HoldingAmount >= HoldingMax ? 0 : HoldingMax - HoldingAmount;
				if(HoldingNeed)
				{
					Need1 = TRUE;
					if(HoldingNeed < Pickup.AmmoGive1)
					{
						item.bSUMMONEDMONSTER = true;
						A_DropItem(Pickup.AmmoType1, Pickup.AmmoGive1 - HoldingNeed);//drop what we don't need
						Pickup.AmmoGive1 = HoldingNeed;
					}
				}
			}
			else
				Need1 = TRUE;
		}
		if(Pickup.AmmoGive2 && Pickup.AmmoType2)
		{
			let Holding = FindInventory(Pickup.AmmoType2);
			if(Holding)
			{
				int HoldingAmount = Holding ? Holding.Amount : 0;
				int HoldingMax = Holding ? Holding.MaxAmount : GetDefaultByType(Holding.GetClass()).MaxAmount;
				int HoldingNeed = HoldingAmount >= HoldingMax ? 0 : HoldingMax - HoldingAmount;
				if(HoldingNeed)
				{
					Need2 = TRUE;
					if(HoldingNeed < Pickup.AmmoGive2)
					{
						item.bSUMMONEDMONSTER = true;
						A_DropItem(Pickup.AmmoType2, Pickup.AmmoGive2 - HoldingNeed);//drop what we don't need
						Pickup.AmmoGive2 = HoldingNeed;
					}
				}
			}
			else
				Need2 = TRUE;
		}
		if(!FindInventory(Pickup.GetClass()))
			NeedGun = TRUE;
		if(NeedGun || Need1 || Need2)
		{
			if(!Need1 && Pickup.AmmoGive1)
			{
				item.bSUMMONEDMONSTER = true;
				A_DropItem(Pickup.AmmoType1, Pickup.AmmoGive1);//drop what we don't need
				Pickup.AmmoGive1 = 0;
			}
			if(!Need2 && Pickup.AmmoGive2)
			{
				item.bSUMMONEDMONSTER = true;
				A_DropItem(Pickup.AmmoType2, Pickup.AmmoGive2);//drop what we don't need
				Pickup.AmmoGive2 = 0;
			}
		}
		return NeedGun || Need1 || Need2;
	}
	bool DoNeedFullPickup(int have, int max, int give, int maxgive)
	{
		bool result;
		if(have >= max || have >= maxgive)
			result = false;
		else if(have + give <= max || have + give <= maxgive)
			result = true;
		return result;
	}
	bool DoNeedPickup(int have, int max, int give, int maxgive)
	{
		if(have >= max || have >= maxgive)
			return false;
		return true;
	}
}