class UnloadedChaingun : UnloadedWeapon
{
	default
	{
		Obituary "You unloaded a chaingun.";
	}
}

class skp_Chaingun : skp_Weapon replaces Chaingun
{
	Default
	{
		Weapon.Slotnumber 4;
		
		Weapon.SelectionOrder 700;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.AmmoType "Clip";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Obituary "$OB_MPCHAINGUN";
		Tag "$TAG_CHAINGUN";
		DropItem "UnloadedChaingun";
		Dropitem "$vend_chaingun", VEND_DESC;
	}
	States
	{
	Ready:
		CGIH A 1 skp_WeaponReady();
		Loop;
	Deselect:
		CGIH A 1 skp_Lower();
		Loop;
	Select:
		CGIH A 1 skp_Raise();
		Loop;
	Fire:
		CGIH E 2 Bright skp_FireChaingunCone();
		CGIH F 1 Bright
		{
			A_Light(2);
			A_ZoomFactor(0.99);
			A_OverlayOffset(1,0,32,WOF_INTERPOLATE);
		}
		CGIH C 1
		{
			A_Light(1);
			A_ZoomFactor(1);
		}
		CGIH B 0 A_ReFire();
		CGIH ABCDDAABBCCCDDDD 1
		{
			A_Light(0);
			A_ZoomFactor(1);
			A_OverlayScale(PSP_WEAPON, player.GetPSprite(PSP_WEAPON).scale.x <= 1 ? 0 : -0.02, 0, WOF_ADD);
			skp_WeaponReady(WRF_NOSWITCH,false,true);//fine to quick attack but not to swap
		}
		Goto Ready;
	Spawn:
		MGUN A -1;
		Stop;
	}
	
	Action void skp_FireChaingunCone()
	{
		A_StartSound("weapons/chngun", CHAN_WEAPON);
		A_ZoomFactor(frandom(0.98,0.985));
		A_Light(3);
		A_OverlayOffset(1,frandom(3,-3),32);
		
		float ShotAng, ShotRad;
		ShotRad = fRandom(0.0,6.32);
		ShotAng = fRandom(0.0,359.9);
		A_FireBullets(ShotRad * Cos(ShotAng), ShotRad * Sin(ShotAng), -1, 5, "BulletPuff",FBF_EXPLICITANGLE|FBF_USEAMMO);
		
		let pspr = player.GetPSprite(PSP_WEAPON);
		pspr.valign = PSPA_TOP;//setting these with the action function wasn't working, easier to set them like this anyway
		pspr.halign = PSPA_CENTER;
		A_OverlayScale(PSP_WEAPON, frandom(1.05, 1.15), 0);
	}
}