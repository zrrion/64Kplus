class UnloadedRocketLauncher : UnloadedWeapon
{
	default
	{
		Obituary "You unloaded a Rocket Launcher.";
	}
}

class skp_RocketLauncher : skp_Weapon replaces RocketLauncher
{
	Default
	{
		Weapon.Slotnumber 5;
		
		Weapon.SelectionOrder 2500;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 2;
		Weapon.AmmoType "RocketAmmo";
		+WEAPON.NOAUTOFIRE
		Inventory.PickupMessage "$GOTLAUNCHER";
		Tag "$TAG_ROCKETLAUNCHER";
		DropItem "UnloadedRocketLauncher";
		Dropitem "$vend_rocketlauncher", VEND_DESC;
	}
	States
	{
	Ready:
		D32R A 1 skp_WeaponReady();
		Loop;
	Deselect:
		D32R A 1 skp_Lower();
		Loop;
	Select:
		D32R A 1 skp_Raise();
		Loop;
	Fire:
		D32R B 3 Bright A_Light(1);
		D32R C 3 Bright;
		D32R D 1 Bright;
		D32R D 2 Bright skp_FireRocket();
		D32R EF 3 Bright
		{
			A_SetPitch(pitch-1,SPF_INTERPOLATE);
			A_ZoomFactor(0.98);
		}
		D32R GGHH 1 Bright
		{
			A_SetPitch(pitch+0.5,SPF_INTERPOLATE);
			A_ZoomFactor(0.995);
		}
		D32R A 1
		{
			A_ZoomFactor(1);
			A_Light(0);
			A_SetPitch(pitch+0.5,SPF_INTERPOLATE);
		}
		TNT1 A 0 A_ReFire();
		Goto Ready;
	Spawn:
		RLOG A -1;
		Stop;
	}
	
	Action void skp_FireRocket()
	{
		A_ZoomFactor(frandom(0.98,0.985));
		A_Light(3);
		A_SetPitch(pitch-0.5,SPF_INTERPOLATE);
		A_ZoomFactor(0.975);
		
		A_FireProjectile("skp_player_rocket");
	}
}

class skp_rocketTrail : Actor
{
	Default
	{
		+NOINTERACTION;
		+NOGRAVITY;
		+NOBLOCKMAP;
	}
	Override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		bXFLIP = random(0,1);
		bYFLIP = random(0,1);
	}
	States
	{
		Spawn:
			TNT1 A 3;
			RPUF ABC 2;
			RPUF DE 2 A_FadeOut();
			RPUF FG 1 A_FadeOut();
			Wait;
	}
}

class skp_rocket : Rocket replaces Rocket
{
	Default
	{
		Decal "Scorch";
		+EXTREMEDEATH;
		-ROCKETTRAIL;
	}
	override int DoSpecialDamage(Actor victim, int damage, Name damagetype)
	{
		if(victim)
			victim.A_GiveInventory("RocketSchorcher", 1);
		
		return Super.DoSpecialDamage(victim, damage, damagetype);
	}
	
	States
	{
	Spawn:
		RCKT A 2 Bright
		{
			Spawn("skp_rocketTrail",self.pos);
		}
		RCKT A 2 Bright
		{	
			Spawn("skp_rocketTrail",self.pos);
			Spawn("RocketSmokeTrail",self.pos, ALLOW_REPLACE);
		}
		Loop;
	Death:
		MISL B 8 Bright
		{
			A_Explode(-1,-1,XF_THRUSTZ);
			A_QuakeEx(1,1,1,60,0,128,"",0,0,0,0,96);
			A_SpawnItemEx("RocketSmokeTrail",0,0,2*(height/3),0,0,fRandom(1,2));
		}
		MISL C 6 Bright;
		MISL D 4 Bright;
		Stop;
	}
}

class RocketSchorcher : StatusEffectParticles
{
	Default
	{
		DropItem "RocketSmokeTrail", -192, -1;
	}
}

class skp_player_rocket : skp_rocket	{	}