class UnloadedChainsaw : UnloadedWeapon
{
	default
	{
		Obituary "You emptied the fuel from a chainsaw.";
	}
}

class skp_Chainsaw : skp_Weapon replaces Chainsaw
{
	Default
	{
		Weapon.Kickback 0;
		Weapon.SelectionOrder 2200;
		Weapon.UpSound "weapons/sawup";
		Inventory.PickupMessage "$GOTCHAINSAW";
		Obituary "$OB_MPCHAINSAW";
		Tag "$TAG_CHAINSAW";
		+WEAPON.MELEEWEAPON
		
		Weapon.Slotnumber 1;
		Weapon.AmmoUse2 1;
		Weapon.AmmoGive2 5;
		Weapon.AmmoType2 "SKP_SawFuel";
		+WEAPON.ALT_AMMO_OPTIONAL;
		DropItem "UnloadedChainsaw";
		Dropitem "$vend_chainsaw", VEND_DESC;
	}
	States
	{
	Ready:
		SAWD ABCD 2
		{
			A_Overlay(666,"Saw.Idle",true);
			if(CountInv("SKP_SawFuel") > 0)
			{
				skp_WeaponReady();
			}
			else
			{
				skp_WeaponReady(WRF_NOSECONDARY);
			}
		}
		Loop;
	Deselect:
		SAWD C 1 skp_Lower();
		Loop;
	Select:
		SAWD C 1 skp_Raise();
		Loop;
	Fire:
		TNT1 A 0 
		{
			A_Overlay(666,"Saw.Idle.End");
			let pspr = player.GetPSprite(PSP_WEAPON);
			pspr.valign = PSPA_TOP;
			pspr.halign = PSPA_CENTER;
			A_OverlayScale(PSP_WEAPON, 1);
		}
		SAWD E 2 A_Saw("weapons/sawfull","weapons/sawhit",1,"BulletPuff",SF_NOPULLIN);
		SAWD F 1
		{
			A_Saw("","",1,"BulletPuff",SF_NOPULLIN);
			A_SpawnItemEx("ChainsawSmoke",2,fRandom(2,6),(height/2)-2,0,fRandom(1,3),fRandom(-2,0));
		}
		SAWD E 1;
		SAWD E 0 A_ReFire();
		SAWD E 0 A_StopSound(CHAN_WEAPON);
		Goto Ready;
	Spawn:
		CSAW A -1;
		Stop;
	}
}

class ChainsawSmoke : skp_RocketSmokeTrail
{
	Default
	{
		alpha 0.25;
		scale 0.5;
	}
}

class ChainsawZerkPuff : skp_BulletPuff
{
	Default
	{
		+EXTREMEDEATH;
		DamageType "GloryKill";
	}
}

class skp_ChainsawPuff_XDeath : skp_FistPuff_XDeath
{
}