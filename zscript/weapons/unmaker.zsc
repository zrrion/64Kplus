class skp_unmaker : skp_Weapon
{
	Default
	{
		//$Category Weapons
		Weapon.Slotnumber 7;
		Weapon.SlotPriority 0;
		
		Weapon.SelectionOrder 2900;
		Weapon.AmmoUse 20;
		Weapon.AmmoGive 80;
		Weapon.AmmoType "Cell";
		+WEAPON.NOAUTOFIRE;
		Obituary "%o was unmade by %k.";
		Inventory.Pickupmessage "What the !@#%* is this!";
		Tag "Unmaykr";
	}
	States
	{
	Ready:
		UMYK A 1 skp_UnmaykrReady();
		UMYK "##" 1 skp_WeaponReady();
		Loop;
	Deselect:
		UMYK A 1 skp_Lower;
		Wait;
	Select:
		UMYK A 1 skp_Raise;
		Wait;
	Fire:
		UMYK A 3 Bright A_StartSound("weapons/UnmakerFire",CHAN_WEAPON);
		UMYK J 4 Bright skp_UnmaykrFire();
		UMYK AAAAAAAAA 1 A_OverlayScale(PSP_WEAPON, player.GetPSprite(PSP_WEAPON).scale.x <= 1 ? 1 : player.GetPSprite(PSP_WEAPON).scale.x-0.02, 0);
		UMYK A 1 A_ReFire();
		Goto Ready;
 	Spawn:
		LSGR A -1;
		Stop;
	}
	action void skp_UnmaykrFire()
	{
		A_FireProjectile("UnmakerBolt");
		
		A_OverlayScale(PSP_WEAPON, 1.18);
		let pspr = player.GetPSprite(PSP_WEAPON);
		pspr.valign = PSPA_TOP;//setting these with the action function wasn't working, easier to set them like this anyway
		pspr.halign = PSPA_CENTER;
	}
	action void skp_UnmaykrReady()
	{
		if(random(0,35) == 0)
		{
			Players[consoleplayer].psprites.frame = random(1,8);
			A_StartSound("world/spark", CHAN_WEAPON, CHANF_OVERLAP, 0.125);
		}
		else if(level.totaltime % 16 == 0)
		{
			Players[consoleplayer].psprites.frame = 0;
			A_StopSound(CHAN_WEAPON);
		}
		skp_WeaponReady();
	}
}

class UnmakerBolt : fastprojectile
{
	Default
	{
		//+RIPPER
		+NOBOSSRIP
		+FORCERADIUSDMG
		+BRIGHT
		+EXTREMEDEATH
		+FORCEPAIN
		+ROLLSPRITE
		+ROLLCENTER
	
		Speed 100;
		Radius 16;
		Height 10;
		DamageFunction (200);
		DamageType "Plasma";//IDK if this will ever do anything
		Decal "BFGScorch";
		Renderstyle "Add";
		MissileType "skp_UnmakerTrail";
	}
	States
	{
		Spawn:
			PLS2 AB 6
			{
				if(Random(0,1) == 0)
				{
					A_SpawnItemEx("skp_UnmakerArc");
				}
			}
			Loop;
		Death:
			LSRU A 6
			{
				roll *= 0;
				A_SpawnItemEx("RocketSmokeTrail",0,0,0,0,0,4);
				A_Explode(128,128,0);
				bXFLIP = random(TRUE,FALSE);
				bYFLIP = random(TRUE,FALSE);
				
				for(int i = 0; i < 3; i++)
				{
					if(Random(0,3) == 0)
					{
						A_SpawnItemEx("skp_UnmakerArc");
					}
				}
			}
			LSRU BCDEF 4
			{
				if(CallACS("CVarCheck",2))
				{
					A_SetRoll(Random(0,359));
				}
			}
			Stop;
		
	}
	Override void PostBeginPlay()
	{
		Frame = random(0,1);
		roll += random(0,1) * 45;
		super.PostBeginPlay();
	}
	Override void Effect()
		{
			class<Actor> trail = MissileName;
			if (trail != null)
			{
				double hitz = pos.z;

				if (hitz < floorz)
				{
					hitz = floorz;
				}
				// Do not clip this offset to the floor.
				hitz += MissileHeight;
				
				Actor act = Spawn (trail, (pos.xy, hitz), ALLOW_REPLACE);
				if (act != null)
				{
					if (bGetOwner && target != null)
						act.target = target;
					else
						act.target = self;
					
					act.frame = frame;
					act.angle = angle;
					act.pitch = pitch;
					act.roll = roll;
				}
			}
		}
}

class skp_UnmakerTrail : actor//ChainsawSmoke : RocketSmokeTrail
{
	Default
	{
		+NOINTERACTION
		+NOGRAVITY
		+NOBLOCKMAP
		+BRIGHT
		+ROLLSPRITE
		+ROLLCENTER
		Renderstyle "Add";
	}
	States
	{
		Spawn:
			PLS2 "#" 1 A_Fadeout();
			Wait;
	}
	Override Void PostBeginPlay()
	{
		A_SpawnItemEX("ChainsawSmoke",frandom(-1,1),frandom(-1,1),frandom(-1,1),frandom(-1,1),frandom(-1,1),frandom(-1,1),0,0,128);
		Super.PostBeginPlay();
	}
}

class skp_UnmakerArc : actor
{
	Default
	{
		+NOINTERACTION
		+NOGRAVITY
		+NOBLOCKMAP
		+ROLLSPRITE
		+WALLSPRITE
		+BRIGHT
		Renderstyle "ADD";
		Scale 0.5;
	}
	States
	{
	Spawn:
		UMSO "#" 2;
		UMSO "#" 1 A_Fadeout(0.2);
		Wait;
	}
	Override void PostBeginPlay()
	{
		A_SetRoll(fRandom(0,359));
		A_SetAngle(fRandom(0,359));
		bXFLIP = random(true,false);
		bYFLIP = random(true,false);
		frame = random(0,1);
		super.PostBeginPlay();
	}
}